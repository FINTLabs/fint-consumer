buildscript {
    apply from: 'https://raw.githubusercontent.com/FINTlibs/fint-buildscripts/v1.0.14/dependencies.gradle'
    ext {
        springBootVersion = springBootVersion
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'com.github.kt3k.coveralls' version '2.8.1'
    id 'com.github.ben-manes.versions' version '0.15.0'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven'
apply plugin: 'jacoco'

apply from: 'https://raw.githubusercontent.com/FINTlibs/fint-buildscripts/v1.0.14/version.gradle'
jar {
    version = createVersion()
    baseName = 'fint-consumer-skeleton'
}

sourceCompatibility = 1.8

import org.apache.tools.ant.filters.ReplaceTokens
afterEvaluate {
    configure(processResources) {
        filesMatching('**/*.yml') {
            filter(ReplaceTokens, tokens: [version: jar.version])
        }
    }
}


repositories {
    mavenLocal()
    jcenter()
    maven {
        url "http://dl.bintray.com/fint/maven"
    }
}

dependencies {
    compile('no.fint:fint-event-model:2.0.0')

    /* --> Models <-- */

    compile('no.fint:fint-relations:1.1.7')
    compile('no.fint:fint-events:1.0.0')
    compile('no.fint:fint-audit-mongo-plugin:1.1.0')
    compile('no.fint:fint-cache:1.0.1')
    compile('no.fint:fint-dependencies:0.0.3')
    
    compile('org.projectlombok:lombok')
    compile("com.github.springfox.loader:springfox-loader:${springfoxLoaderVersion}")

    compile('org.springframework.boot:spring-boot-starter-web')
    runtime('org.springframework.boot:spring-boot-devtools')

    testCompile('no.fint:fint-test-utils:0.0.4')
    testCompile('cglib:cglib-nodep:3.2.5')
    testCompile("org.spockframework:spock-spring:${spockSpringVersion}")
    testCompile("org.spockframework:spock-core:${spockSpringVersion}")
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleVersion
}

bootRun {
    systemProperties System.properties
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

apply from: 'https://raw.githubusercontent.com/FINTlibs/fint-buildscripts/v1.0.14/dependencyReport.gradle'
